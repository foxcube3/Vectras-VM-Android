plugins {
    alias(myLibs.plugins.android.application)
}

android {
    namespace = 'com.vectras.vm.shellloader' // Added namespace
    compileSdk = rootProject.ext.compileApi

    defaultConfig {
        minSdkVersion = rootProject.ext.minApi
        applicationId = "com.vectras.vm.shellloader"
        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "logTag", "\"Termux:X11 loader\""
        buildConfigField "String", "CLASS_ID", "\"com.vectras.vm.x11.CmdEntryPoint\""
        buildConfigField "String", "packageNotInstalledErrorText",
                """\"Termux:X11 application is not found.\" +\"You should choose latest successful workflow here: https://github.com/termux/termux-x11/actions/workflows/debug_build.yml\" +\"After this you should download \\"termux-x11-ARCH-debug\\" or \\"termux-x11-universal-debug\\" artifact and install apk contained in this zip file.\""""
        buildConfigField "String", "packageSignatureMismatchErrorText",
                "\"Signature verification of target application " + android.namespace + " failed.\\nPlease, reinstall both termux-x11 package and Termux:X11 application from the same source\""
        buildConfigField "String", "COMMIT", "\"" + ("git rev-parse HEAD\n".execute().getText().trim() ?: (System.getenv('CURRENT_COMMIT') ?: "NO_COMMIT")) + "\""
        buildConfigField "int", "SIGNATURE", "-1" // Placeholder for signature
    }

    buildTypes {
        debug {
            signingConfig = null
        }
    }

    buildFeatures {
        buildConfig = true
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFileName = "loader.apk"
        }
    }
}

dependencies {
    compileOnly(project(':shell-loader:stub'))
}
